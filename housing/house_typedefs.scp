//****************************************************************************
// SphereServer by: SphereServer development team and Menasoft.
// www.sphereserver.net
//****************************************************************************
VERSION=X1

// House sign override
[TYPEDEF ei_house_sign]
On=@Create
	timerf 1, trigger @HouseSysInit // The LINK is automatically set immediately after placement and @Create

On=@HouseSysInit
	if <link.isvalid> && !<link.tag0.convert>
		attr = 010
		timer = -1
	else
		if <def.hs_can_decay>
			attr = 02|010
			timer = 60*60*24*<def.hs_can_decay>
		else
			attr = 010
			timer = -1
		endif
	endif
	if <link.isvalid>
		name = <link.name>
		link.region.events +r_house_system
		link.region.flags = region_flag_nodecay|region_flag_nobuilding|region_antimagic_recall_in|region_flag_insta_logout
		
		ref1 = <uid> //sign
		ref2 = <link> //house
		ref3 = <link.owner> //owner
		
		call f_house_init 01
		call f_house_counter_set
		
		local.file_name = secure/secure
		LOGOPEN <local.file_name>
		LOGWRITE <local.file_name>,[BUILD] House <link.name>[<link.uid>] build by <ref3.name>(<ref3.account>)[<ref3.uid>]
		LOGFLUSH <local.file_name>
		resendtooltip 1,0
	endif

On=@Click
	ref4 = <link.region.uid>
	if !<ref4.tag0.forsale>
		if (<link.housetype> == <def.house_guild>)
			message <name>
			message [<uid.<tag0.is_guild>.abbrev>]
			return 1
		else
			return 0
		endif
	else
		message For Sale
		message <link.dtag0.price> gp
		return 1
	endif

On=@ClientToolTip
	if (<link> == <def.link_invalid>)
		return 0
	endif
	ref4 = <link.region.uid>
	if <link.tag0.demolition>
		addcliloc 1070722, Demolition Scheduled in <QVAL (<eval <timer>/86400> < 1)? <eval (<timer>%86400)/3600> hours and <eval ((<timer>%86400)%3600)/60> minutes.:<eval <timer>/86400> days>
	elseif <ref4.tag0.forsale>
		addcliloc 1070722, FOR SALE <link.dtag0.price>gp
	endif
	addcliloc 1042971,<link.name>
	addcliloc 1061639,<name>
	addcliloc 1061640,<link.owner.name>
	if (<link.housetype> == <def.house_guild>)
		addcliloc 1042971,[<uid.<tag0.is_guild>.abbrev>] Guild House
	endif
	addcliloc <QVAL (<link.housetype>==<def.house_private>)? 1061642:1061641>
	return 1

On=@DClick
	if (<link> != <def.link_invalid>)
		if !(<def.hs_can_decay>)
			if (<attr> & 02)
				attr &= ~02
				timer = -1
			endif
		else
			if !(<attr> & 02)
				attr |= 02
				if (<timer> < 0)
					timer = 60*60*24*<def.hs_can_decay>
				endif
			endif
		endif
	endif
	if (<link.isowner <src>>) || (<link.GetCoownerPos <src>> >= 0) || (<link.GetFriendPos <src>> >= 0) || (<src.isgm>)
		if (<src.region.uid> == <link>)
			link.sdialog d_house_menu
			return 1
		else
			src.sysmessage @32,,1 You must be standing on your doorstep to access the house menu.
			return 1
		endif
	else
		if !<link.region.tag0.forsale>
			link.sdialog d_house_visitor
			return 1
		else
			link.sdialog d_house_forsale
			return 1
		endif
	endif

On=@Timer
	ref2 = <link.owner>
	if (<def.hs_can_decay>)
		attr = 012
		if (<link>)
			link.redeed
		endif
		if <ref2.isonline>
			ref2.sysmessage @,,1 Your <link.name> in <region.region.name> has collapsed
			ref2.sysmessage @,,1 The number of houses you own is now <ref2.houses>
		endif
	else
		return 1
	endif


// Event for locked down items.
[TYPEDEF ei_house_lockdown]
On=@ClientToolTip
	addcliloc 501643

On=@ContextMenuRequest
	ref1 = <region.uid>
	if (<src> == <ref1.owner>) || (<tag0.lockedby> == <src>) || <src.isgm>
		src.AddContextEntry 701,1015183	// Unlock
	endif

On=@ContextMenuSelect
	if (<distance> > 3)
		src.sysmessage @,,1 You can't reach <name>.
		return 1
	endif
	if (<argn> == 701)
		ref1 = <region.uid>
		ref1.unlockitem <uid>
		tag.lockedby =
		message @,,2 501726	// No longer locked down!
		if !<isempty <tag.decay>>
			tag.decay =
			timer <eval(<serv.DecayTimer>*60)>
			attr |= attr_decay
		endif
		resendtooltip 1,0
	endif

On=@PickUp_Ground   // Do not allow to move this item
	if (!<src.IsGM>)
		return 1
	endif

On=@PickUp_Self // For containers only, do not allow to pick up items
	if (!<src.IsGM>)
		return 1
	endif

// When entering customize mode, all locked down items and containers are being moved to the Moving Crate
// but they are still Locked Down items, counting as so and having their properties
// so they can't be moved from ground nor items can be dragged from them
// but they can still be picked up from the moving crate and dropped on the ground or other containers
// that's why some checks must be done to prevent malintentionated/unintended placements outside the multi.
On=@DropOn_Ground
	if (<region.uid> != <link.region.uid>)
		return 1
	endif

On=@DropOn_Item
	if (<argo.region.uid> != <link.region.uid>)
		return 1
	endif

// Doors event 
[TYPEDEF ei_house_door]
On=@Click
	if (<tag0.access>==house_access_owner)
		message @,,1 Owner Only
	elif (<tag0.access>==house_access_coowner)
		message @,,1 Co-Owners Only
	elif (<tag0.access>==house_access_friend)
		message @,,1 Friends Only
	elif (<tag0.access>==house_access_guild)
		message @,,1 [<uid.<tag0.is_guild>.abbrev>] Members Only
	endif
	message @,,1 <name>
	return 1

On=@ContextMenuRequest
	ref1=<region.uid>
	if <src>==<ref1.owner> || <src.isgm>
		src.AddContextEntry 700,1078864	// Access
	endif

On=@ContextMenuSelect
	if <argn> == 700
		ref1=<uid>
		dialogclose d_house_secure
		ref1.sdialog d_house_secure
	endif

On=@ClientToolTip
	if (<tag0.access>==house_access_owner)
		addcliloc 1060658,Access,<def.bfont_lyellow>Owner Only<def.bfont_white>
	elif (<tag0.access>==house_access_coowner)
		addcliloc 1060658,Access,<def.bfont_lyellow>Co-Owners<def.bfont_white>
	elif (<tag0.access>==house_access_friend)
		addcliloc 1060658,Access,<def.bfont_lyellow>Friends<def.bfont_white>
	elif (<tag0.access>==house_access_guild)
		ref1=<tag.is_guild>
		if <ref1.isvalid>
			addcliloc 1114773,<def.bfont_lyellow>[<ref1.abbrev>]<def.bfont_white>,Guild Members Only
		endif
	endif

On=@DClick
	ref1 = <region.uid>
	if <def.hs_can_decay> 
		if (<ref1.tag0.decay_exempt>)
			ref1.link.timer = -1
		else
			if !<def.hs_property_tax>
				if (<ref1.isowner <src>>)
					if !(<ref1.tag0.demolition>)
						ref1.link.timer = 60*60*24*<def.hs_can_decay>
					else
						src.sysmessage @32,,1 This house is improperly placed and has been scheduled for demolition in  <QVAL (<eval <ref1.link.timer>/86400> < 1)? <eval (<ref1.link.timer>%86400)/3600> hours and <eval ((<ref1.link.timer>%86400)%3600)/60> minutes.:<eval <ref1.link.timer>/86400> days.>
						src.sysmessage @32,,1 Unless you redeed it yourself before that time the house and all items within will be lost without refund.
					endif
				endif
			else
				if (<ref1.tag0.demolition>)
					src.sysmessage @32,,1 This house is improperly placed and has been scheduled for demolition in  <QVAL (<eval <ref1.link.timer>/86400> < 1)? <eval (<ref1.link.timer>%86400)/3600> hours and <eval ((<ref1.link.timer>%86400)%3600)/60> minutes.:<eval <ref1.link.timer>/86400> days.>
					src.sysmessage @32,,1 Unless you redeed it yourself before that time the house and all items within will be lost without refund.
				endif
			endif
		endif
	endif
	if (<serv.AutoHouseKeys>)   // If keys are used, let the server check them.
		return 0
	endif
	if (<type> == t_door_locked)
		type = t_door
	endif
	if (<f_house_can_access>)
		UseDoor
		return 1
	endif

// House container created from COMPONENTs from [MULTIDEF ] (this also have 'ei_house_component').
[TYPEDEF ei_house_container]

// House Component created from [MULTIDEF ].
[TYPEDEF ei_house_component]

// Event for Moving Crates.
// Moving Crates are meant for House Customizing purposes, some items are moved inside when required
// in customize mode. Player storage is NOT allowed.
// Moving Crates are automatically deleted when there are no items inside.
// *More behaviour on player side event (e_moving_crate).
[TYPEDEF ei_moving_crate]
if !<src.isgm> //ошибка???
	return 1
endif

On=@PickUp_Self
	if <count> <= 1
		remove
	endif

On=@Destroy
	ref1 = <link.owner>
	if (<ref1> && <ref1.isevent.e_moving_crate>)
		ref1.events -e_moving_crate
	endif

On=@Create
	attr |= <def.attr_invis>

// Secured Containers.
// Only allowed players can manage their storage (Refer to f_house_can_access to see the allowance).
[TYPEDEF ei_house_secure]
On=@Clienttooltip
	addcliloc 501644
	if (<tag0.access>==house_access_owner)
		addcliloc 1060658,Access,<def.bfont_lyellow>Owner Only<def.bfont_white>
	elif (<tag0.access>==house_access_coowner)
		addcliloc 1060658,Access,<def.bfont_lyellow>Co-Owners<def.bfont_white>
	elif (<tag0.access>==house_access_friend)
		addcliloc 1060658,Access,<def.bfont_lyellow>Friends<def.bfont_white>
	elif (<tag0.access>==house_access_guild)
		ref1=<tag.is_guild>
		if <ref1.isvalid>
			addcliloc 1114773,<def.bfont_lyellow>[<ref1.abbrev>]<def.bfont_white>,Guild Members Only
		endif
	endif

On=@ContextMenuRequest
	ref1=<region.uid>
	if <src>==<ref1.owner> || <src.isgm>
		src.AddContextEntry 700,1078864	// Access
		src.AddContextEntry 701,1015183	// Unlock
	endif

On=@ContextMenuSelect
	if <distance> > 3
		src.sysmessage @,,1 You can't reach <name>.
		return 1
	endif
	if <argn> == 700
		dialogclose d_house_secure
		sdialog d_house_secure
	elif <argn> == 701
		ref1=<region.uid>
		ref1.release <uid>
		message @,,2 501718	// No longer secure!
		if !<isempty <tag.decay>>
			tag.decay=
			timer <eval <serv.DecayTimer>*60>
			attr |= attr_decay
		endif
		resendtooltip 1,0
	endif

On=@PickUp_ground
	if (!<src.IsGM>)
		 return 1
	endif

On=@DClick
	if (<f_house_can_access>)
		 return 0
	endif
	return 1

On=@PickUp_Self
	if (<f_house_can_access>)
		 return 0
	endif
	return 1

// Telepad for multis.
[TYPEDEF ei_house_telepad]
On=@Click
	if (<tag0.access>==house_access_owner)
		message @,,1 Owner Only
	elif (<tag0.access>==house_access_coowner)
		message @,,1 Co-Owners Only
	elif (<tag0.access>==house_access_friend)
		message @,,1 Friends Only
	elif (<tag0.access>==house_access_guild)
		message @,,1 [<uid.<tag0.is_guild>.abbrev>] Members Only
	endif
	message @,,1 <name>
	return 1

On=@ClientToolTip
	if (<tag0.access> == house_access_owner)
		addcliloc 1060658,Access,<def.bfont_lyellow>Owner Only<def.bfont_white>
	elif (<tag0.access> == house_access_coowner)
		addcliloc 1060658,Access,<def.bfont_lyellow>Co-Owners<def.bfont_white>
	elif (<tag0.access> == house_access_friend)
		addcliloc 1060658,Access,<def.bfont_lyellow>Friends<def.bfont_white>
	elif (<tag0.access> == house_access_guild)
		ref1=<tag.is_guild>
		if <ref1.isvalid>
			addcliloc 1114773,<def.bfont_lyellow>[<ref1.abbrev>]<def.bfont_white>,Guild Members Only
		endif
	endif

On=@Step
	if !(<link> == <def.link_invalid>) 
		if !(<src.isgm>)
			if (<f_house_can_access>)
				return 0
			endif
			return 1
		endif
	endif

// @Custom event for house transfers
[TYPEDEF t_trade_house_deed]
On=@Click
	message House Transfer Contract
	message House Name: <LINK.NAME>
	MESSAGE Owner: <UID.<LINK.MORE1>.NAME>
	MESSAGE Location: <LINK.SEXTANTP> //<LINK.REGION.REGION.NAME> (<LINK.P>)
	return 1

On=@ClientToolTip
	addcliloc 1061112,<LINK.NAME>
	addcliloc 1061113,<UID.<LINK.MORE2>.NAME>
	addcliloc 1061114,<STRARG <LINK.SEXTANTP>> <STREAT <LINK.SEXTANTP>>

On=@Timer
	if (<cont.type> == t_eq_trade_window)
		timerf 2,trigger @timer
	else
		uid.<more1>.events -e_house_transfer
		remove
	endif
	return 1

On=@HouseTraded
	ref1 = <more1>		//New Owner
	ref2 = <link.owner>	//Old Owner
	ref1.events -e_house_transfer
	ref2.events -e_house_transfer
	link.tag.lasttransfer = <serv.rtime>
	ref2.delhouse <uid>
	link.owner = <ref1>
	for <eval(<ref2.count>-1)> 0
		if (<ref2.findcont.<local._for>.link> == <link.link>)
			ref2.findcont.<dlocal._for>.cont = <ref1>
		endif
	endfor
	ref2.sysmessage @,,1 You transfer the property to <ref1.name>
	ref2.sysmessage @,,1 The number of houses you own is now <ref2.houses>.
	ref1.sysmessage @,,1 <ref2.name> has transferred ownership of this property to you.
	ref1.sysmessage @,,1 The number2 of houses you own is now <ref1.houses>.
	link.delban -1
	link.delcoowner -1
	link.delaccess -1
	link.delfriend -1
	link.resendtooltip 1

// @Override of t_deed typedef
[TYPEDEF t_deed]
On=@DClick
	if (<cont> != <src.findlayer.21>)
		src.sysmessage @,,1 The item must be on your backpack.
		return 1
	endif
	if (<dispid> == i_gold)
		timerf 60,trigger @targon_cancel
	endif

On=@TargOn_Ground
	IF !(<SRC.ISGM>)
		IF !STRMATCH("*ship*","<BASEID>")
			IF (<SRC.TAG0.House.PlacementDelay> > <SERV.TIME>)
				SRC.SYSMESSAGE @,,1 Number of days until you can place another house: <eval (<SRC.TAG0.House.PlacementDelay>-<SERV.TIME>)/864000>
				return 1
			ELIF !(<DEF0.PLACEMENT_ILSHENAR_FACET>) && (<SRC.TARGP.M>==2)
				SRC.SYSMESSAGE @,,1 Housing cannot be created in this area.
				TRIGGER @TargOn_Cancel
				return 1
			ENDIF
		ENDIF
		SRC.TIMERF 1,f_house_isPlaced <MORE1> <BASEID> <SRC.TARGP>
	ENDIF

On=@TargOn_Char
	SRC.SYSMESSAGE @,,1 You must place your house on ground.
	TRIGGER @TargOn_Cancel
	return 1

On=@TargOn_Item
	SRC.SYSMESSAGE @,,1 You must place your house on ground.
	TRIGGER @TargOn_Cancel
	return 1

On=@TargOn_Cancel
	if (<dispid> == i_gold)
		src.sdialog d_house_placement_tool
		remove
	endif

// @Custom region event for houses.
[REGIONTYPE r_house_system]
On=@Enter
	if <src.isplayer>
		src.events +e_house_player_events
		// SRC.dSPEECH +spk_player_house
		// src.timerf 1, f_house_visitor_count
		ref1 = <uid>
		if !<src.isgm>
			if <ref1.housetype> == <def.house_private>
				if (<ref1.isowner <src>>) || (<ref1.getcoownerpos <src>> >= 0) || (<ref1.getfriendpos <src>> >= 0) || (<ref1.GetAccessPos <src>> >= 0) || (<src.isgm>)
					return 0
				elif (<ref1.housetype> == <def.house_guild>)
					if (<src.guild> == <ref1.tag0.is_guild>)
						return 0
					else
						src.sysmessage @32,,1 You are not a member of this guild
						return 1
					endif
				else
					src.sysmessage @32,,1 This is private property.
					src.sysmessage @32,,1 You may not trespass.
					return 1
				endif
			else
				if (<ref1.getbanpos <src>> >= 0)
					src.sysmessage @32,,1 You are banned from this property
					return 1
				else
					return 0
				endif
			endif
		endif
	endif

On=@Exit
	src.dialogclose d_house_menu 
	src.events -e_house_player_events

//////////////////////////////////////////////////////////////////////////////////////events///////////////////////////////////////////////////
[EVENTS e_house_count_block]
On=@AddMulti
	if <argn2> != <def.hp_owner>
		return 1 // Prevents this multi from being added to SRC's house storage
	endif

[EVENTS e_house_transfer]
On=@TradeAccepted
	dialogclose d_house_demolish
	dialogclose d_house_menu
	if <argn1>
		for <argn1>
			if <REF<dLOCAL._FOR>>
				if (<REF<dLOCAL._FOR>.TYPE>==t_trade_house_deed)
					UID.<REF<dLOCAL._FOR>>.TRIGGER @HouseTraded
					UID.<REF<dLOCAL._FOR>>.REMOVE
				endif
			endif
		endfor
	endif
	EVENTS=-e_house_transfer

[EVENTS e_house_player_events]
On=@ItemDClick
	if (<act.defname> == <def.vendor_deed_id>)
		ref1 = <region.uid>
		if !<def.house_list_vendors>
			if <ref1.isowner <uid>> || (<src.isgm)
				if (<ref1.housetype> == house_private)
					src.sysmessage @32,,1 You must set your building to public before you can place vendors here.
					return 1
				else
					if (<ref1.vendors> < <ref1.maxvendors>)
						return 0
					else
						sysmessage @32,,1 You have no available vendor slots
						sysmessage @32,,1 You must remove an existing vendor before you can place a new one.
						return 1
					endif
				endif
			else
				sysmessage @32,,1 Only the home owner may place vendors here.
				return 1
			endif
		else
			if (<ref1.isowner <src>>) || (<ref1.getcoownerpos <src>> >= 0) || (<ref1.getfriendpos <src>> >= 0) || (<src.isgm)
				if (<ref1.housetype> == house_private)
					src.sysmessage @32,,1 You must set your building to public before you can place vendors here.
					return 1
				else
					if (<ref1.vendors> < <ref1.maxvendors>)
						return 0
					else
						sysmessage @32,,1 You have no available vendor slots
						sysmessage @32,,1 You must remove an existing vendor before you can place a new one.
						return 1
					endif
				endif
			else
				sysmessage @32,,1 Only people on the home lists may place a vendor here.
				return 1
			endif
		endif
	endif

On=@ContextMenuRequest
	ref1 = <region.uid>
	if (<uid> == <src>) && <ref1.link>
		src.addcontextentry 101,3006207
	endif

On=@ContextMenuSelect
	if (<argn> == 101)
		ref1 = <region.uid>
		src.go <uid.<ref1.link>.p>
	endif

[EVENTS e_house_customize]
On=@SkillStart
	if !(<housedesign>)
		return 2
	endif
	sysmessage @32,,1 You cannot do this whilst designing a house.
	return 1

On=@SkillUseQuick
	if !(<housedesign>)
		return 2
	endif
	return 1

// On=@HouseDesignCommitItem
// serv.log <local.id> --- <local.p.x> ---- <local.p.y> ------ <local.p.z> ------- <local.visible>

On=@HouseDesignCommit
	if (<isgm>)
		sysmessage @,,1 Your new house design has been committed.
		return 2
	endif
	local.oldcost = <eval(<argn1> * 500)>
	local.newcost = <eval(<argn2> * 500)>
	local.curcost = <eval(<local.newcost> - <local.oldcost>)>
	if (<gold> < <local.curcost>)
		sysmessage @32,,1 You cannot afford this house design.
		return 1
	endif
	argo.tag0.construction = <eval(<local.newcost>-<argo.value>)>
	sysmessage @,,1 Your new house design has been committed.
	if (<local.curcost> == 0)
		sysmessage @,,1 As the new design costs the same as the previous one, no gold has been taken out of your account.
	elif (<local.curcost> < 0)
		local.curcost = <eval(0 - <local.curcost>)>
		gold += <local.curcost>
		sysmessage @,,1 As the new design is cheaper than the previous one, <dlocal.curcost> gold has been returned to you.
	else
		gold -= <local.curcost>
		sysmessage @,,1 <dlocal.curcost> gold has been taken out of your account to pay for the construction.
	endif
	return 2

On=@HouseDesignExit
	sysmessage @,,1 You have left house design mode.
	events -e_house_customize
	if <argo.MovingCrate>
		ref1 = <argo.MovingCrate>
		ref1.attr |= attr_invis
		ref1.p = <argo.p>
		ref1.move 0,0,-20
	endif
	return 2

[EVENTS e_moving_crate]
On=@LogOut
	for 0 <eval(<houses>-1)>
		ref1 = <house.<dlocal._for>.MovingCrate>
		if (<ref1>)
			ref1.attr |= attr_invis
			ref1.p <ref1.link.p>
			ref1.move 0 0 -20
		endif
	endfor
	events -e_moving_crate

On=@HouseDesignBegin
	for 0 <eval(<houses>-1)>
		ref1 = <house.<dlocal._for>.MovingCrate>
		if (<ref1>)
			ref1.attr |= attr_invis
			ref1.p <ref1.link.p>
			ref1.move 0 0 -20
		endif
	endfor
	events -e_moving_crate

// Event applied when you are listed (with any privilege) on one or more houses.
[EVENTS e_house_priv]

// Event applied when you are listed (with any privilege) on one or more ships.
[EVENTS e_ship_priv]

[EOF]
